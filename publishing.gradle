apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

tasks.register("sourceJar", Jar) {
    from android.sourceSets.main.java.srcDirs
    archiveClassifier.set('sources')
}

afterEvaluate {
    publishing {
        publications {
            releaseAar(MavenPublication) {
                from components.release
                artifact sourceJar

                groupId = project.ext.groupId
                artifactId = project.ext.artifactId
                version = rootProject.ext.versionName

                pom {
                    name = 'View Binding Property Delegate'
                    description = 'Make work with Android View Binding simpler'
                    url = 'https://github.com/kirich1409/ViewBindingPropertyDelegate'
                    licenses {
                        license {
                            name = 'The Apache License, Version 2.0'
                            url = "http://www.apache.org/licenses/LICENSE-2.0.txt"
                        }
                    }
                    developers {
                        developer {
                            id = 'kirich1409'
                            name = 'Kirill Rozov'
                            email = 'krl.rozov@gmail.com'
                        }
                    }
                    scm {
                        url = 'https://github.com/kirich1409/ViewBindingPropertyDelegate'
                    }
                }
            }
        }
    }

    bintray {
        user = rootProject.hasProperty('user') ? rootProject.property('user') : System.getenv('BINTRAY_USER')
        key = rootProject.hasProperty('apiKey') ? rootProject.property('apiKey') : System.getenv('BINTRAY_API_KEY')

        publications = ["releaseAar"]

        pkg {
            repo = 'maven'
            name = project.ext.bintrayPackageName
            desc = "Make work with Android View Binding simpler"
            userOrg = System.env.BINTRAY_USER_ORGANIZATION ?: ""
            licenses = ['Apache-2.0']
            vcsUrl = 'https://github.com/kirich1409/ViewBindingPropertyDelegate.git'
            websiteUrl = 'https://github.com/kirich1409/ViewBindingPropertyDelegate'
            issueTrackerUrl = 'https://github.com/kirich1409/ViewBindingPropertyDelegate/issues'
            labels = ['view binding', 'kotlin']

            githubRepo = 'kirich1409/ViewBindingPropertyDelegate' //Optional Github repository

            version {
                name = rootProject.ext.versionName
                vcsTag = rootProject.ext.versionName
            }
        }
    }
}
